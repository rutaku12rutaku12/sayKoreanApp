plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.5'
    id 'io.spring.dependency-management' version '1.1.7'
    id "com.github.node-gradle.node" version "7.0.1" // 최신 호환 버전 확인 권장
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'
description = 'Demo project for Spring Boot'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // [1] 스프링 웹
    implementation 'org.springframework.boot:spring-boot-starter-web'
    // [2] 롬복
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    // [3] mysql jdbc
    runtimeOnly 'com.mysql:mysql-connector-j'
    // [4] websocket
    implementation 'org.springframework.boot:spring-boot-starter-websocket'
    // [5] JSOUP
    implementation 'org.jsoup:jsoup:1.21.2'
    // [6] Selenium
    implementation 'org.seleniumhq.selenium:selenium-java:4.35.0'
    // [7] 셀레니움 브라우저 매니저 ( 크롬 )
    implementation 'io.github.bonigarcia:webdrivermanager:6.3.1'
    // [8] 마이바티스
    implementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter:3.0.5'
    // [9] AOP
    implementation 'org.springframework.boot:spring-boot-starter-aop'
    // [10] validation (dto 에서 유효성 검사 )
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    // [11] Google Cloud Translation API - 자동 번역
    implementation 'com.google.cloud:google-cloud-translate:2.30.0'
    // [12] Google Cloud Text-to-Speech API - TTS
    implementation 'com.google.cloud:google-cloud-texttospeech:2.78.0'
    // [13] Google Gemini API - AI 채점
    implementation 'com.google.cloud:google-cloud-aiplatform:3.35.0'
    // [*] JSON 처리 (이미 있을 수 있음)
    implementation 'com.fasterxml.jackson.core:jackson-databind'
    // [*] 테스트
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    // [14] Gemini api
    implementation("com.google.genai:google-genai:1.24.0")
    // [15] crizin (발음기호 라이브러리)
    implementation 'net.crizin:korean-romanizer:2.0.1' // Maven Central 기준
    // [16] Springdoc OpenAPI 3(Swagger)
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.7.0'
    // [17] 리캡차
    implementation 'net.tanesha.recaptcha4j:recaptcha4j:0.0.7'
    // [18] 리캡차 사용을 위한 json
    implementation 'javax.json:javax.json-api:1.1.2'
    implementation 'org.glassfish:javax.json:1.1'
    // [19] Bcrypt
    implementation 'org.springframework.security:spring-security-crypto:6.4.4'
    // [20] 스프링 시큐리티
    implementation 'org.springframework.boot:spring-boot-starter-security'
    // [21] oauth2
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
    // [22] Apache POI (Excel 생성)
    implementation 'org.apache.poi:poi:5.2.5' // 최신 안정 버전 권장
    implementation 'org.apache.poi:poi-ooxml:5.2.5' // 최신 안정 버전 권장
    // [23] JPA
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    // [24] J(자바)JWT 라이브러리
    implementation 'io.jsonwebtoken:jjwt-api:0.12.6'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.6'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.6'
    // [25] SMTP
    implementation 'org.springframework.boot:spring-boot-starter-mail'


}

tasks.named('test') {
    useJUnitPlatform()
}

// --- React 빌드 관련 Task 정의 (이전과 동일) ---

// 1. npm install 실행 Task (React 의존성 설치)
task npmInstallReact(type: NpmTask) {
    description = "Install Node.js dependencies from package.json"
    workingDir = file("${project.projectDir}/src/main/saykorean") // <<<--- 작업 디렉토리 명시적 지정
    args = ['install']
    // dependsOn npmCacheClean // 이전 오류 라인 (필요시 복구/수정)
    inputs.file("src/main/saykorean/package.json")
    inputs.file("src/main/saykorean/package-lock.json") // 또는 yarn.lock
    outputs.dir("src/main/saykorean/node_modules")
}

// 2. npm run build 실행 Task (React 앱 빌드)
task npmBuildReact(type: NpmTask) {
    description = "Build the React application using npm run build"
    dependsOn npmInstallReact
    workingDir = file("${project.projectDir}/src/main/saykorean")
    args = ['run', 'build']
    inputs.dir("src/main/saykorean/src")
    inputs.dir("src/main/saykorean/public")
    inputs.file("src/main/saykorean/package.json")
    // *** 사용하는 React 빌드 도구에 맞춰 'dist' 또는 'build'로 수정하세요! ***
    outputs.dir("src/main/saykorean/dist")
}

// 3. 리액트가 실행되고 실행된 결과물을 스프링의 resources 폴더에 복제
// 스프링 환경에 리액트 빌드(실행) 결과를 넣어서 스프링 환경에서 리액트 통합
// 스프링 : 8080 , 리액트 : 5173  : 2개의 서버를 스프링8080 으로 통합
task copyReactBuild(type: Copy) {
    description = "Copy React build output to Spring Boot static resources"
    dependsOn npmBuildReact
    // *** 사용하는 React 빌드 도구에 맞춰 'dist' 또는 'build'로 수정! ***
    from "${project.projectDir}/src/main/saykorean/dist"
    into "${project.buildDir}/resources/main/static"
}

// --- Clean Task (이전과 동일) ---
clean {
    delete file("${project.projectDir}/src/main/saykorean/node_modules")
    // *** 사용하는 React 빌드 도구에 맞춰 'dist' 또는 'build'로 수정! ***
    delete file("${project.projectDir}/src/main/saykorean/dist")
}

// --- Task 의존성 설정 (이전과 동일) ---
processResources {
    dependsOn copyReactBuild
}

